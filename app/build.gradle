apply plugin: 'com.android.application'

def keystoreFile = rootProject.file("app/sp.jks")

android {
    if (keystoreFile.exists()) {
        signingConfigs {
            release {
                storeFile file("sp.jks")
                storePassword "smartpack"
                keyAlias "sp_key1"
                keyPassword "smartpack"
            }
        }
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    compileSdk 34
    namespace 'com.sunilpaulmathew.debloater'

    defaultConfig {
        applicationId "com.sunilpaulmathew.debloater"
        minSdkVersion 23
        targetSdk 34
        versionCode 27
        versionName "v0.27"
        vectorDrawables.useSupportLibrary = true
        testFunctionalTest = false
        testHandleProfiling = false

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            if (keystoreFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig = true
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation 'com.github.sunilpaulmathew:RootFilePicker:802663ee90'

    def sCommon_Version = '8c5666a002'
    implementation "com.github.sunilpaulmathew.sCommon:adapters:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:apkutils:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:credits:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:fileutils:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:jsonutils:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:packageutils:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:permissionutils:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:themeutils:${sCommon_Version}"
    implementation "com.github.sunilpaulmathew.sCommon:translatorutils:${sCommon_Version}"

    implementation "com.github.topjohnwu.libsu:core:4.0.3"
    implementation 'com.google.android.material:material:1.12.0'
}